plugins {
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
    id "com.github.johnrengelman.shadow" version "4.0.2"
    id "groovy"
    id "jacoco"
    id "checkstyle"
    id "codenarc"
    id 'com.github.kt3k.coveralls' version '2.8.4'
    id "maven-publish"
    id "com.jfrog.bintray" version "1.8.4"
    id "net.ltgt.apt-eclipse" version "0.21"
    id "net.ltgt.apt-idea" version "0.21"
}



version "0.2.3"
group "com.agorapulse"

repositories {
    mavenCentral()
    maven { url "https://jcenter.bintray.com" }
}

dependencyManagement {
    imports {
        mavenBom 'io.micronaut:micronaut-bom:1.2.0'
    }
}

configurations {
    optional.extendsFrom compile

    testCompile.extendsFrom optional
}

dependencies {
    // custom
    compile 'com.restfb:restfb:2.26.0'

    testCompile "com.agorapulse:gru-http:0.8.2"

    testCompile "io.micronaut:micronaut-http-server-netty"

    // generated
    annotationProcessor "io.micronaut:micronaut-inject-java"
    annotationProcessor "io.micronaut:micronaut-validation"

    compile "io.micronaut:micronaut-inject"
    compile "io.micronaut:micronaut-validation"
    compile "io.micronaut:micronaut-runtime"

    optional "io.micronaut:micronaut-http"
    optional "io.reactivex.rxjava2:rxjava"

    runtime "ch.qos.logback:logback-classic:1.2.3"

    testAnnotationProcessor "io.micronaut:micronaut-inject-java"

    testCompile("org.spockframework:spock-core") {
        exclude group: "org.codehaus.groovy", module: "groovy-all"
    }

    testCompile "io.micronaut:micronaut-inject-groovy"
    testCompile "io.micronaut.test:micronaut-test-spock"
    testCompile 'net.bytebuddy:byte-buddy:1.10.1'
    testCompile 'org.objenesis:objenesis:3.0.1'
}

tasks.withType(JavaCompile){
    options.encoding = "UTF-8"
    options.compilerArgs.add('-parameters')
}

checkstyle {
    toolVersion = '8.23'
}

codenarc {
    toolVersion = '1.4'
}


// publishing
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        groovyMaven(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}

// set bintrayUser & bintrayKey in gradle.properties
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['groovyMaven']
    publish = true

    final String projectName = project.name
    final String projectDescription = project.description

    pkg {
        websiteUrl = 'https://github.com/agorapulse/micronaut-facebook-sdk'
        issueTrackerUrl = 'https://github.com/agorapulse/micronaut-facebook-sdk/issues'
        vcsUrl = 'https://github.com/agorapulse/micronaut-facebook-sdk.git'

        repo = 'libs' // or your repo name
        userOrg = 'agorapulse'
        name = projectName       // somehow project.* doesn't work in this closure
        desc = projectDescription
        licenses = ['Apache-2.0']
    }
    // dryRun = true // whether to run this as dry-run, without deploying
}

